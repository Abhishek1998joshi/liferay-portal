@component-name = "portal-solutions"
definition {

	property osgi.modules.includes = "osb-site-initializer-customer-portal";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "OSB Site Initializer Customer Portal";

	var testSiteName = "Customer Portal Site";
	var testSiteURL = "customer-portal-site";
	var testSiteTamplate = "Liferay OSB Site Initializer Customer Portal";
	var testUser = "Test Test";
	var userFirstName = "Test";
	var testAccount = "Test Account DXP";
	var testProduct = "DXP";

    setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Customer Portal Site") {
			CustomerPortal.addCustomerPortalSite(siteName = "${testSiteName}");
		}

		task ("Add test user access to all Accounts") {
			CustomerPortal.assignUserToOrganization();

			CustomerPortal.addUserToOneOrMoreAccounts(accountsList = "${testAccount}");
		}

		task ("Login on OKTA") {
			CustomerPortal.loginOktaToUpdateCP();
		}

		task ("Go to Customer Portal Site") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.waitCP20PageToLoad(userFirstName = "${userFirstName}");
		}

		task ("I go to the test account") {
			CustomerPortal.search(searchItem = "${testAccount}");

			CustomerPortal.enterOnProject(title = "${testAccount}");
		}

		task ("I click on DXP product") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "${testProduct}");
		}

		task ("I generate a new key") {
			CP2ActivationKeys.generateNewKey();
		}

		task ("I fulfill first phase form for Generate New License") {
			CP2ActivationKeys.selectGenerateActivationKeyOptionByField(
				field = "Version",
				option = "7.4");

			CP2ActivationKeys.selectGenerateActivationKeyOptionByField(
				field = "Key Type",
				option = "Production");

			CP2ActivationKeys.selectFistActiveSubscriptionInput();

			CustomerPortal.clickAtButton(text = "Next");
		}
    }

    tearDown {

		var testPortalInstance = PropsUtil.get("test.portal.instance");

		JSONGroup.deleteGroupByName(groupName = "${testSiteName}");

		CustomerPortal.customerPortalApisTearDown();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Automation ID: LPS-157445 | Test Summary: Verify if the fields (Environment Name - Host Name) are required"
	@priority = "5"
	test CanViewAMandatoryMessageForBothFields {
		task ("Given I go to next form at Genarate New Key") {
			CustomerPortal.clickAtButton(text = "Next");
		}

		task ("And The Generate button should be deactivated") {
			CustomerPortal.assertButtonIsDisabled(text = "Generate 1 Key");
		}

		task ("And I fulfill the Environment and Host name fields") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "Environment Name",
				content = "Environment Test Name");

			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "Host Name",
				content = "localhost");
		}

		task ("Then The Generate button should be active") {
			CustomerPortal.assertButtonIsActive(text = "Generate 1 Key");
		}

		task ("When I erase the Environment Name field content") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "Environment Name",
				content = "");
		}

		task ("Then I should see the alert with the text 'This field is required.'") {
			CustomerPortal.assertErrorMessageIsVisible(errorMessage = "This field is required.");
		}

		task ("And The Generate button should be deactivated") {
			CustomerPortal.assertButtonIsDisabled(text = "Generate 1 Key");
		}

		task ("When I erase the Host Name field content") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "Host Name",
				content = "");
		}

		task ("Then The Generate button should be deactivated") {
			CustomerPortal.assertButtonIsDisabled(text = "Generate 1 Key");
		}
	}

	@description = "Automation ID: LPS-157446 | Test Summary: Verify if the user enters an invalid IP address and Mac Address an error message appears"
	@priority = "3"
	test CanViewTheErrorMessageForInvalidIPAddressAndMACAddress {
		task ("Given I active IP and MAC Address field") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "IP Address",
				content = "");

			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "MAC Address",
				content = "");
		}

		task ("Given I fulfill the IP Address field with a invalid IP") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "IP Address",
				content = "1234567789");
		}

		task ("Then I should see the alert with the text 'Invalid IP.'") {
			CustomerPortal.assertErrorMessageIsVisible(errorMessage = "Invalid IP.");
		}

		task ("Given I fulfill the IP Address field with a IP with white spaces") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "IP Address",
				content = "192.168 .1.1");
		}

		task ("Then I should see the alert with the text 'The IP most not have spaces.'") {
			CustomerPortal.assertErrorMessageIsVisible(errorMessage = "The IP must not have spaces.");
		}

		task ("When I fulfill the IP Address field with a valid IP") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "IP Address",
				content = "192.168.1.1");
		}

		task ("Then I should not see the alert with the text 'Invalid IP.'") {
			CustomerPortal.assertErrorMessageIsNotVisible(errorMessage = "Invalid IP.");
		}

		task ("Then I should not see the alert with the text 'The IP must not have spaces.'") {
			CustomerPortal.assertErrorMessageIsNotVisible(errorMessage = "The IP must not have spaces.");
		}

		task ("When I fulfill the MAC Address field with a invalid MAC") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "MAC Address",
				content = "InvalidMACAddress");
		}

		task ("Then I should see the alert with the text 'Invalid Mac.'") {
			CustomerPortal.assertErrorMessageIsVisible(errorMessage = "Invalid MAC.");
		}

		task ("When I fulfill the MAC Address field with a MAC with white spaces") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "MAC Address",
				content = "A0-B1-C2- D3-E4-F5");
		}

		task ("Then I should see the alert with the text 'The Mac must not have spaces.'") {
			CustomerPortal.assertErrorMessageIsVisible(errorMessage = "The MAC must not have spaces.");
		}

		task ("When I fulfill the MAC Address field with a valid MAC") {
			CP2ActivationKeys.fulfillGenerateActivationKeyTextField(
				field = "MAC Address",
				content = "A0-B1-C2-D3-E4-F5");
		}

		task ("Then I should not see the alert with the text 'Invalid MAC.'") {
			CustomerPortal.assertErrorMessageIsNotVisible(errorMessage = "Invalid MAC.");
		}
	}
}