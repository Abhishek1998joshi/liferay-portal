import com.liferay.gradle.util.OSDetector
import com.liferay.gradle.util.StringUtil

import java.text.DateFormat
import java.text.SimpleDateFormat

import java.util.Date
import java.util.TimeZone

import org.gradle.util.GUtil

apply plugin: "com.liferay.node"

configurations {
	remoteApps
}

task buildRemoteApps
task forceDeploy
task zipRemoteApps(type: Zip)

DateFormat dateFormat = new SimpleDateFormat("yyyyMMdd.HHmm")

dateFormat.setTimeZone(TimeZone.getTimeZone("UTC"))

String dateString = dateFormat.format(new Date())

artifacts {
	remoteApps zipRemoteApps
}

dependencies {
	remoteApps ext: "zip", group: "com.liferay", name: "com.liferay.site.initializer.raylife.remote.apps", version: "20211011.1724"
}

forceDeploy {
	finalizedBy deploy
}

node {
	global = false
	nodeVersion = "14.17.6"
	useNpm = false
}

processResources {
	ResolvedConfiguration remoteAppsResolvedConfiguration = configurations.remoteApps.getResolvedConfiguration()

	if (gradle.startParameter.taskNames.contains("forceDeploy") || remoteAppsResolvedConfiguration.hasError()) {
		dependsOn buildRemoteApps

		into("site-initializer/documents/remote-apps") {
			from new File(buildDir, "remote-apps")
		}
	}
	else {
		into("site-initializer/documents/remote-apps") {
			from zipTree(configurations.remoteApps.singleFile)
		}
	}
}

uploadRemoteApps {
	doLast {
		File buildGradleFile = file("build.gradle")

		String content = buildGradleFile.getText("UTF-8")

		content = content.replaceFirst(~/remoteApps ext: "zip", group: "com.liferay", name: "com.liferay.site.initializer.raylife.remote.apps", version: "(\w+\.\w+)"/) {
			String all, String versionString ->

			all.replace versionString, dateString
		}

		buildGradleFile.setText content, "UTF-8"
	}

	repositories {
		mavenDeployer {
			pom {
				artifactId = GUtil.loadProperties(file("bnd.bnd"))["Bundle-SymbolicName"] + ".remote.apps"
				groupId = "com.liferay"
				version = dateString
			}
		}
	}
}

zipRemoteApps {
	dependsOn buildRemoteApps

	archiveName "remote-apps.zip"
	destinationDir new File(buildDir, "dist")
	from new File(buildDir, "remote-apps")
}

_createRemoteAppsTasks()

private void _createRemoteAppsTasks() {
	File remoteAppsDir = new File(projectDir, "remote-apps")

	remoteAppsDir.eachDir {
		File dir ->

		String suffix = dir.name.replaceAll(/[\.-](\w)/) {
			return it[1].toUpperCase()
		}

		Task yarnInstallTask = tasks.create(name: "yarnInstall" + StringUtil.capitalize(suffix), type: Exec) {
			if (OSDetector.windows) {
				executable "cmd.exe"

				args "/c"
				args new File(node.nodeDir, "node.exe")
				args new File(node.nodeDir, "node_modules/yarn/yarn-" + node.yarnVersion + ".js")
			}
			else {
				executable new File(node.nodeDir, "bin/node")

				args new File(node.nodeDir, "lib/node_modules/yarn/yarn-" + node.yarnVersion + ".js")
			}

			args "install"
			dependsOn downloadNode
			workingDir dir
		}

		Task yarnBuildTask = tasks.create(name: "yarnBuild" + StringUtil.capitalize(suffix), type: Exec) {
			if (OSDetector.windows) {
				executable "cmd.exe"

				args "/c"
				args new File(node.nodeDir, "node.exe")
				args new File(node.nodeDir, "node_modules/yarn/yarn-" + node.yarnVersion + ".js")
			}
			else {
				executable new File(node.nodeDir, "bin/node")

				args new File(node.nodeDir, "lib/node_modules/yarn/yarn-" + node.yarnVersion + ".js")
			}

			args "build"
			dependsOn yarnInstallTask
			workingDir dir

			doLast {
				copy {
					from new File(dir, "build/static")
					include "js/*.js"
					into new File(buildDir, "remote-apps/" + dir.name)
				}
			}
		}

		buildRemoteApps.finalizedBy yarnBuildTask
	}
}