import aQute.bnd.version.Version

import com.liferay.gradle.util.OSDetector
import com.liferay.gradle.util.StringUtil

import org.gradle.util.GUtil

apply plugin: "com.liferay.node"

configurations {
	extra
}

task buildExtra
task forceDeploy
task zipExtra(type: Zip)

Version extraZipVersion = Version.parseVersion("0.0.0")

artifacts {
	extra zipExtra
}

dependencies {
	extra ext: "zip", group: "com.liferay", name: "com.liferay.site.initializer.raylife.extra", version: extraZipVersion
}

forceDeploy {
	finalizedBy deploy
}

classes {
	dependsOn buildExtra
}

node {
	global = false
	nodeVersion = "14.17.6"
	useNpm = false
}

processResources {
	if (gradle.startParameter.taskNames.contains("forceDeploy") || (extraZipVersion.major < 1)) {
		dependsOn buildExtra

		into("site-initializer/documents/extra") {
			from new File(buildDir, "extra")
		}
	}
	else {
		into("site-initializer/documents/extra") {
			from zipTree(configurations.extra.singleFile)
		}
	}
}

uploadExtra {
	repositories {
		mavenDeployer {
			pom {
				artifactId = GUtil.loadProperties(file("bnd.bnd"))["Bundle-SymbolicName"] + ".extra"
				groupId = "com.liferay"
				version = extraZipVersion
			}
		}
	}
}

zipExtra {
	dependsOn buildExtra

	archiveName "extra.zip"
	destinationDir new File(buildDir, "dist")
	from new File(buildDir, "extra")
}

_createExtraTasks()

private void _createExtraTasks() {
	File extraDir = new File(projectDir, "extra")

	extraDir.eachDir {
		File dir ->

		String suffix = dir.name.replaceAll(/[\.-](\w)/) {
			return it[1].toUpperCase()
		}

		Task yarnInstallTask = tasks.create(name: "yarnInstall" + StringUtil.capitalize(suffix), type: Exec) {
			if (OSDetector.windows) {
				executable "cmd.exe"

				args "/c"
				args new File(node.nodeDir, "node.exe")
				args new File(node.nodeDir, "node_modules/yarn/yarn-" + node.yarnVersion + ".js")
			}
			else {
				executable new File(node.nodeDir, "bin/node")

				args new File(node.nodeDir, "lib/node_modules/yarn/yarn-" + node.yarnVersion + ".js")
			}

			args "install"
			dependsOn downloadNode
			workingDir dir
		}

		Task yarnBuildTask = tasks.create(name: "yarnBuild" + StringUtil.capitalize(suffix), type: Exec) {
			if (OSDetector.windows) {
				executable "cmd.exe"

				args "/c"
				args new File(node.nodeDir, "node.exe")
				args new File(node.nodeDir, "node_modules/yarn/yarn-" + node.yarnVersion + ".js")
			}
			else {
				executable new File(node.nodeDir, "bin/node")

				args new File(node.nodeDir, "lib/node_modules/yarn/yarn-" + node.yarnVersion + ".js")
			}

			args "build"
			dependsOn yarnInstallTask
			workingDir dir

			doLast {
				copy {
					from new File(dir, "build/static")
					include "js/*.js"
					into new File(buildDir, "extra/" + dir.name)
				}
			}
		}

		buildExtra.dependsOn yarnBuildTask
	}
}