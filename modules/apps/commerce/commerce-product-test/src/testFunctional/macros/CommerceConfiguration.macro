definition {

	macro accountGroupsTearDown {
		var accountGroupsList = "";
		var count = "1";

		while (IsElementPresent(key_rowNumber = "${count}", locator1 = "CommerceNavigation#ENTRY_NAME_N")) {
			var key_rowNumber = "${count}";
			var entryName = selenium.getText("CommerceNavigation#ENTRY_NAME_N");

			if ("${entryName}" != "Guest") {
				var accountGroupsList = StringUtil.add("${accountGroupsList}", "${entryName}");
			}

			var count = ${count} + 1;
		}

		if ("${accountGroupsList}" != "") {
			for (var accountGroupName : list "${accountGroupsList}") {
				ClickNoError(
					key_entryName = "${accountGroupName}",
					locator1 = "CommerceEntry#ENTRY_NAME_BODY_VERTICAL_ELLIPSIS_2");

				CommerceEntry.clickNoError(menuItem = "Delete");

				AssertElementPresent(locator1 = "Message#SUCCESS");

				AssertElementNotPresent(locator1 = "Message#ERROR");
			}
		}
	}

	macro commerceAdminSetUp {
		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.addCP(
			userEmailAddress = "commerceuser@liferay.com",
			userFirstName = "Commerce",
			userLastName = "User",
			userScreenName = "commerceuser");

		CommerceEntry.createNewCurrenciesEntry(
			code = "NCU",
			entryName = "New Currency");

		CommerceNavigator.gotoPortlet(
			category = "Store Management",
			portlet = "Channels");

		Click(locator1 = "Button#PLUS");

		CommerceEntry.addNewChannelsEntry(
			channelCurrency = "US Dollar",
			channelName = "Test Channel",
			channelType = "Site",
			siteName = "Guest");

		Button.clickSave();

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}

	macro commerceAdminTearDown {
		var emptyCount = "0";

		Navigator.openURL();

		CommerceJSONOrdersAPI._deleteAllCommerceOrders();

		var orderCount = CommerceJSONOrdersAPI._getCommerceOrdersCount();

		TestUtils.assertEquals(
			actual = "${orderCount}",
			expected = "0");

		CommerceJSONShipmentsAPI._deleteAllCommerceShipments();

		var shipmentsCount = CommerceJSONShipmentsAPI._getCommerceShipmentsCount();

		TestUtils.assertEquals(
			actual = "${shipmentsCount}",
			expected = "${emptyCount}");

		JSONUser.tearDownNonAdminUsers();

		CommerceJSONAccountsAPI._deleteAllCommerceAccounts();

		var accountsCount = CommerceJSONAccountsAPI._getCommerceAccountsCount();

		TestUtils.assertEquals(
			actual = "${accountsCount}",
			expected = "${emptyCount}");

		CommerceJSONAccountGroupsAPI._deleteAllCommerceAccountGroups();

		var accountGroupsCount = CommerceJSONAccountGroupsAPI._getCommerceAccountGroupsCount();

		TestUtils.assertEquals(
			actual = "${accountGroupsCount}",
			expected = "${emptyCount}");

		CommerceJSONProductsAPI._deleteAllCommerceProducts();

		var productsCount = CommerceJSONProductsAPI._getCommerceProductsCount();

		TestUtils.assertEquals(
			actual = "${productsCount}",
			expected = "${emptyCount}");

		CommerceJSONOptionsAPI._deleteAllCommerceOptions();

		var optionsCount = CommerceJSONOptionsAPI._getCommerceOptionsCount();

		TestUtils.assertEquals(
			actual = "${optionsCount}",
			expected = "${emptyCount}");

		CommerceJSONSpecificationGroupsAPI._deleteAllCommerceSpecificationGroups();

		var specificationGroupsCount = CommerceJSONSpecificationGroupsAPI._getCommerceSpecificationGroupsCount();

		TestUtils.assertEquals(
			actual = "${specificationGroupsCount}",
			expected = "${emptyCount}");

		CommerceJSONSpecificationsAPI._deleteAllCommerceSpecifications();

		var specificationsCount = CommerceJSONSpecificationsAPI._getCommerceSpecificationsCount();

		TestUtils.assertEquals(
			actual = "${specificationsCount}",
			expected = "${emptyCount}");

		CommerceJSONChannelsAPI._deleteAllCommerceChannels();

		var channelsCount = CommerceJSONChannelsAPI._getCommerceChannelsCount();

		TestUtils.assertEquals(
			actual = "${channelsCount}",
			expected = "${emptyCount}");

		CommerceJSONCatalogsAPI._deleteAllNoSystemCommerceCatalogs();

		var noSystemCatalogIds = CommerceJSONCatalogsAPI._getCommerceNoSystemCatalogIds();

		TestUtils.assertEquals(
			actual = "${noSystemCatalogIds}",
			expected = "");

		CommerceJSONPriceListsAndPromotionsAPI._deleteAllNoMasterPriceListsAndPromotions();

		var priceListAndPromotionIds = CommerceJSONPriceListsAndPromotionsAPI._getCommerceNoMasterPriceListsAndPromotions();

		TestUtils.assertEquals(
			actual = "${priceListAndPromotionIds}",
			expected = "");

		CommerceJSONDiscountsAPI._deleteAllCommerceDiscounts();

		var discountsCount = CommerceJSONDiscountsAPI._getCommerceDiscountsCount();

		TestUtils.assertEquals(
			actual = "${discountsCount}",
			expected = "${emptyCount}");

		CommerceJSONAvailableEstimatesAPI._deleteCommerceAvailableEstimates();

		var availableEstimatesCount = CommerceJSONAvailableEstimatesAPI._getAvailableEstimatesCount();

		TestUtils.assertEquals(
			actual = "${availableEstimatesCount}",
			expected = "${emptyCount}");

		CommerceJSONMeasurementUnitsAPI._deleteCommerceMeasurementUnits();

		var availableMeasurementUnits = CommerceJSONMeasurementUnitsAPI._getCommerceAvailableMeasurementUnitsCount();

		TestUtils.assertEquals(
			actual = "${availableMeasurementUnits}",
			expected = "${emptyCount}");

		CommerceJSONTaxCategoriesAPI._deleteCommerceTaxCategories();

		var commerceTaxCategoriesIds = CommerceJSONTaxCategoriesAPI._getCommereTaxCategoriesCount();

		TestUtils.assertEquals(
			actual = "${commerceTaxCategoriesIds}",
			expected = "${emptyCount}");

		CommerceJSONWarehousesAndInventoriesAPI._deleteAllWarehouseItems();

		var warehousesCount = CommerceJSONWarehousesAndInventoriesAPI._getCommerceWarehousesCount();

		TestUtils.assertEquals(
			actual = "${warehousesCount}",
			expected = "${emptyCount}");
	}

	macro commerceInitializersTearDown {
		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		CommerceJSONSitesAPI._deleteCommerceSites();
	}

	macro commerceStoreFrontSetUp {
		CommerceNavigator.gotoPortlet(
			category = "Settings",
			portlet = "Health Check");

		for (var buttonRowNumber : list "1,2,3,4,5") {
			if (!(IsElementPresent(key_buttonStatus = "disabled", locator1 = "CommerceEntry#FIX_ISSUE_BUTTON_N"))) {
				Click(
					key_buttonRowNumber = "${buttonRowNumber}",
					key_buttonStatus = "",
					locator1 = "CommerceEntry#FIX_ISSUE_BUTTON_N");

				WaitForElementPresent(
					key_value = "${buttonRowNumber}",
					locator1 = "CommerceEntry#HEALTH_CHECK_ICON_CHECK_N");
			}
		}

		CommerceNavigator.gotoPortlet(
			category = "Store Management",
			portlet = "Channels");

		Click(locator1 = "Button#PLUS");

		CommerceEntry.addNewChannelsEntry(
			channelCurrency = "US Dollar",
			channelName = "Test Channel",
			channelType = "Site",
			siteName = "Guest");

		Button.clickSave();

		AssertElementPresent(locator1 = "Message#SUCCESS");

		Click(
			key_menuTab = "General",
			locator1 = "CommerceEntry#MENU_TAB");

		for (var fixIssueButtonName : list "Product Detail,Product Compare,Commerce Cart,Checkout") {
			if (IsElementPresent(key_fixIssueButtonName = "${fixIssueButtonName}", locator1 = "CommerceEntry#FIX_ISSUE_BUTTON")) {
				Click(
					key_fixIssueButtonName = "${fixIssueButtonName}",
					locator1 = "CommerceEntry#FIX_ISSUE_BUTTON");

				Alert.viewSuccessMessage();
			}
		}
	}

	macro commerceStoreFrontTearDown {
		Navigator.openURL();

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Catalog");

		if (IsElementNotPresent(locator1 = "CommerceNavigation#EMPTY_SEARCH_CONTAINER")) {
			CommerceProducts.moveCatalogEntriesToRecycleBin();
		}

		CommerceNavigator.gotoPortlet(
			category = "Settings",
			portlet = "Options");

		Pause(locator1 = "3000");

		CommerceOptions.deleteOptionsEntries();

		User.logoutPG();
	}

	@summary = "Deletes if present, catalogs, price lists or promotions, excluding Master occurrencies"
	macro commerceTearDownExcludingMaster {
		var elementsList = "";
		var count = "1";

		while (IsElementPresent(key_rowNumber = "${count}", locator1 = "CommerceNavigation#ENTRY_NAME_N")) {
			var key_rowNumber = "${count}";
			var entryName = selenium.getText("CommerceNavigation#ENTRY_NAME_N");

			if (("${entryName}" != "Master") && ("${entryName}" != "Master Base Price List") && ("${entryName}" != "Master Base Promotion")) {
				var elementsList = StringUtil.add("${elementsList}", "${entryName}");
			}

			var count = ${count} + 1;
		}

		if ("${elementsList}" != "") {
			for (var catalogName : list "${elementsList}") {
				ClickNoError(
					key_entryName = "${catalogName}",
					locator1 = "CommerceEntry#ENTRY_NAME_BODY_VERTICAL_ELLIPSIS");

				CommerceEntry.clickNoError(menuItem = "Delete");

				AssertElementPresent(locator1 = "Message#SUCCESS");

				AssertElementNotPresent(locator1 = "Message#ERROR");
			}
		}
	}

	macro enableDiagramFeature {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		Click(
			key_text = "Catalog",
			locator1 = "Link#ANY");

		Click(
			key_text = "Diagram Product Type",
			locator1 = "Link#ANY");

		Click.clickAtNotVisible(
			checkboxName = "Enabled",
			locator1 = "Checkbox#ANY_CHECKBOX");

		Button.clickSave();

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}

	macro reindexAllSearchIndexes {
		SearchAdministration.executeReindex();
	}

}