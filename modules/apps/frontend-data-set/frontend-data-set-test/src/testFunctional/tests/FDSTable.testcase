@component-name = "portal-frontend-infrastructure"
definition {

	property custom.properties = "upgrade.database.auto.run=false";
	property osgi.modules.includes = "frontend-data-set-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Frontend Dataset";
	property testray.main.component.name = "Frontend Dataset";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		task ("Given Frontend Dataset sample portlet") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				widgetName = "Frontend Data Set Sample");

			JSONLayout.updateLayoutTemplateOfPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				layoutTemplate = "1 Column");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");
		}
	}

	@description = "LPS-165727. Assert the details shown in the FDS table"
	@priority = "4"
	test HeadersCanBeLocalized {
		task ("And Given: site URL localized to Spanish //localhost:8080/es/*") {
			var portalURL = PropsUtil.get("portal.url");

			Navigator.openURL(baseURL = "${portalURL}/es/home");

			Navigator.gotoPage(pageName = "Frontend Data Set Test Page");
		}

		task ("When: on classic tab view") {
			Click(
				key_sampleTab = "Clásico",
				locator1 = "FrontendDataSet#FDS_DATASET");
		}

		task ("Then: There is no hyphen in any header text") {
			AssertTextNotPresent(
				key_itemName = "Nombre",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW",
				value1 = "-");
		}

		task ("And Then: First letter is capitalized for all headers and localized Spanish headers are present") {
			AssertTextEquals.assertTextCaseSensitive(
				key_field = "Nombre",
				locator1 = "FrontendDataSet#TABLE_ITEM_COLUMN",
				value1 = "Nombre");

			AssertTextEquals.assertTextCaseSensitive(
				key_field = "Apellido",
				locator1 = "FrontendDataSet#TABLE_ITEM_COLUMN",
				value1 = "Apellido");

			AssertTextEquals.assertTextCaseSensitive(
				key_field = "Dirección de correo",
				locator1 = "FrontendDataSet#TABLE_ITEM_COLUMN",
				value1 = "Dirección de correo");
		}
	}

}