definition {

	macro assertFileNameFromTempFolder {
		var translationZipFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "${webContentTitle}*.zip");

		echo("## translationZipFileName: ${translationZipFileName}");

		if (contains("${translationZipFileName}", "en_US")) {
			echo("This is a exported translation file.");
		}
		else {
			fail("FAIL!");
		}
	}

	macro exportTranslation {
		AssertElementPresent(locator1 = "Language#EXPORT_TRANSLATION_ORIGIN_LANGUAGE");

		AssertElementPresent(
			key_checkboxName = "${defaultLanguage}",
			locator1 = "Checkbox#DISABLED_EXPORT_TRANSLATION");

		AssertElementPresent(locator1 = "Button#EXPORT_MODAL_DISABLED");

		for (var languageList : list "${languageList}") {
			Check.checkNotVisible(
				checkboxName = "${languageList}",
				locator1 = "Checkbox#ANY_CHECKBOX");
		}

		Click(locator1 = "Button#EXPORT_MODAL");

		DownloadTempFile();
	}

	macro gotoExportTranslation {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click.waitForMenuToggleJSClick(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_LIST_MENULIST_ICON");

		MenuItem.click(menuItem = "Export for Translation");
	}

	macro gotoImportTranslation {
		Click.waitForMenuToggleJSClick(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_LIST_MENULIST_ICON");

		MenuItem.click(menuItem = "Import Translation");
	}

	macro importTranslation {
		Translations.gotoImportTranslation(webContentTitle = "${webContentTitle}");

		AssertClick(
			locator1 = "Button#SELECT_FILE",
			value1 = "Select File");

		UploadTempFile.uploadTempFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = "${fileName}");

		if ("${workflow}" == "true") {
			PortletEntry.submitForPublication();
		}

		else if ("${workflowResubmit}" == "true") {
			PortletEntry.save();
		}

		else {
			PortletEntry.publish();
		}
	}

	macro translateFile {
		AntCommand(
			locator1 = "build-test.xml",
			value1 = "update-translation-file -Dtranslation.content.target.original=<target><![CDATA[${webContentContent}]]></target> -Dtranslation.content.target=<target><![CDATA[${contentTranslateion}]]></target> -Dtranslation.description.target.original=<target><![CDATA[${webContentDescription}]]></target> -Dtranslation.description.target=<target><![CDATA[${descriptionTranslateion}]]></target> -Dtranslation.title.target.original=<target><![CDATA[${webContentTitle}]]></target> -Dtranslation.title.target=<target><![CDATA[${titleTranslation}]]></target> -Dtranslation.zip.file.name=${translationZipFileName} -Dtranslation.file.name=${translationFileName} -Dtranslation.file.rename=${translationFileRename}");
	}

	macro viewTranslationOptions {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click.waitForMenuToggleJSClick(
			key_webContentTitle = "${webContentTitle}",
			locator1 = "WC#ENTRY_LIST_MENULIST_ICON");

		if (isSet(noOptions)) {
			MenuItem.viewNotPresent(menuItem = "Translate");

			MenuItem.viewNotPresent(menuItem = "Export for Translation");

			MenuItem.viewNotPresent(menuItem = "Import Translation");
		}
		else {
			MenuItem.viewPresent(menuItem = "Translate");

			MenuItem.viewPresent(menuItem = "Export for Translation");

			MenuItem.viewPresent(menuItem = "Import Translation");
		}
	}

}