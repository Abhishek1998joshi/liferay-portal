@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Depot";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		JSONStaging.enableLocalStaging(
			groupName = "Test Depot Name",
			site = "false");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONDepot.deleteDepot(depotName = "Test Depot Name");

			User.logoutPG();
		}
	}

	@description = "This test covers LPS-155796. It ensures that an image that was published in a depot can still be accessed after local staging is disabled."
	@priority = "5"
	test ImageIsStillAccessibleAfterDisablingStaging {
		property osgi.module.configuration.file.names = "com.liferay.document.library.configuration.FFFriendlyURLEntryFileEntryConfiguration.config";
		property osgi.module.configurations = "enabled=B&quot;true&quot;";

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Image Description",
			dmDocumentTitle = "Document_1",
			groupName = "Test Depot Name-staging",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		DepotNavigator.openToEditDMEntryInDepot(
			depotName = "Test Depot Name",
			dmDocumentTitle = "Document_1",
			staging = "true");

		DMDocument.editFriendlyURL(
			dmDocumentTitle = "Document_1",
			friendlyURLEdit = "document_1_edit");

		DepotNavigator.openDepotDocumentsAndMediaAdminStaging(depotName = "Test Depot Name");

		DepotNavigator.switchStagingStatus(stagingItem = "Publish to Live");

		Staging.publishToLive();

		DepotNavigator.openToDMEntryInDepot(
			depotName = "Test Depot Name",
			dmDocumentTitle = "Document_1");

		var latestVersionURL = DMDocument.getLatestVersionURL();

		Depot.disableStagingDepot(depotName = "Test Depot Name");

		Navigator.openSpecificURL(url = "${latestVersionURL}");

		AssertVisible(
			key_imageSourceURL = "document_1_edit",
			locator1 = "DocumentsAndMediaDocument#DOCUMENT_VIEWPORT_PREVIEW");
	}

}