@component-name = "portal-frontend-infrastructure"
definition {

	property osgi.modules.includes = "frontend-js-components-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Translation Manager";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "JS Components Sample Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Guest",
			layoutName = "JS Components Sample Page",
			widgetName = "JS Components Sample");

		Navigator.gotoPage(pageName = "JS Components Sample Page");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();

			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "JS Components Sample Page");
		}
	}

	@description = "LPS-147499. Verifies default options are present in translation manager language menu."
	@priority = "3"
	test DefaultLanguageIsAvailableAndViewsNoOtherLanguagesOptions {
		task ("Select translation language menu") {
			TranslationManager.openSelectTranslationLanguageDropdownMenu(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("Assert only default language options are available") {
			AssertElementPresent(
				key_locale = "en-US",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(
				key_locale = "ca-ES",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(
				key_locale = "fr-FR",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(locator1 = "TranslationManager#MANAGE_TRANSLATIONS");

			AssertElementNotPresent(
				key_index = "6",
				locator1 = "TranslationManager#SELECT_TRANSLATION_LANGUAGE_MENU_ITEM_INDEX");
		}

		task ("Assert link to Manage Translations is available") {
			Click(locator1 = "TranslationManager#MANAGE_TRANSLATIONS");

			AssertElementPresent(locator1 = "Portlet#MODAL_TITLE");
		}
	}

	@description = "LPS-147946. Verifies the list will filter the results and the language will be deleted from the Manage Translation list."
	@priority = "3"
	test SearchAndDeleteLanguageOnManageTranslationModal {
		task ("Select translation language menu") {
			TranslationManager.openSelectTranslationLanguageDropdownMenu(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("Click Manage Translations and type text in search field") {
			Click(locator1 = "TranslationManager#MANAGE_TRANSLATIONS");

			Type.type(
				locator1 = "TranslationManager#MANAGE_TRANSLATIONS_DIALOG_SEARCH_FIELD",
				value1 = "English");
		}

		task ("View the list will filter the results using Code and Language fields") {
			AssertElementPresent(
				key_text = "Code",
				locator1 = "TranslationManager#MANAGE_TRANSLATIONS_DIALOG_SEARCH_RESULTS_TITLE");

			AssertElementPresent(
				key_text = "Language",
				locator1 = "TranslationManager#MANAGE_TRANSLATIONS_DIALOG_SEARCH_RESULTS_TITLE");

			Click(locator1 = "Modal#CLOSE_BUTTON");
		}

		task ("Delete language in Manage Translations dialog") {
			TranslationManager.deleteLanguageFromDialogList(
				key_currentLocale = "en-us",
				key_locale = "ca-es",
				key_title = "Admin");
		}
	}

	@description = "LPS-147612. Verifies translation results displayed by different language."
	@priority = "3"
	test TranslationResultsDisplayedByDifferentLanguage {
		task ("Input different languages with the same word") {
			TranslationManager.useDifferentLanguageInputText(
				key_currentLocale = "en-us",
				key_locale = "fr-FR",
				key_title = "Admin",
				languagefirst = "Welcome to Liferay",
				languagesecond = "Bienvenue sur Liferay");
		}

		task ("View translation results displayed by different language and the language selector is changed to the selected language") {
			AssertElementPresent(
				key_currentLocale = "fr-fr",
				locator1 = "TranslationManager#SELECT_TRANSLATION_LANGUAGE_AUI_TAG_ADMINMENU");

			AssertElementPresent(
				key_title = "Admin",
				locator1 = "TranslationManager#AUI_TAG_INPUT_TEXT_FIELD",
				value1 = "Bienvenue sur Liferay");

			AssertElementPresent(
				key_title = "Admin",
				locator1 = "TranslationManager#AUI_TAG_DEFAULT_LANGUAGE_TEXT",
				value1 = "Welcome to Liferay");
		}
	}

}