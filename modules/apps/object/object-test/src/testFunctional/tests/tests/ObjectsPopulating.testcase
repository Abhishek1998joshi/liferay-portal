@component-name = "portal-object"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Object";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			ObjectAdmin.deleteAllCustomObjectsViaAPI();
		}
	}

	@description = "LPS-136451 - Verify if it's possible to see the entries of a field with capitalized letters in the name"
	@priority = "4"
	test FieldWithCapitalizedLetters {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "ObjectsPopulating#FieldWithCapitalizedLetters";

		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object",
			objectName = "CustomObject",
			pluralLabelName = "Custom Objects");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldLabelName = "Not Required Field",
			fieldName = "dueDate",
			fieldType = "Date",
			isRequired = "false",
			objectName = "CustomObject");

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject");

		PopulateObjectFormsConfiguration.openForm();

		FormsAdminNavigator.gotoAddForm();

		Form.editName(formName = "Form 1");

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		PopulateObjectFormsConfiguration.selectCustomObject(label = "C_CustomObject");

		Form.gotoAddField(fieldType = "Date");

		PopulateObjectFormsConfiguration.mapFormField(fieldName = "dueDate");

		Form.save();

		Form.publishForm();

		FormsAdminNavigator.gotoPublishedForm();

		FormViewBuilder.editDate(
			fieldName = "Date",
			fieldValue = "01/01/2001");

		FormPortlet.submitSuccessfully();

		Navigator.openURL();

		ObjectAdmin.goToCustomObject(objectName = "CustomObject");

		ObjectsPopulating.viewEntriesObjectPortlet(
			entries = "2001-01-01",
			fieldName = "dueDate");
	}

}