@component-name = "data-engine"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Data Engine";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Geolocation",
			fieldName = "Geolocation");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-98071. As a Developer, I want to Implement the geolocation field type"
	@priority = "5"
	test EditSearchableOfGeolocation {
		var key_structureName = "WC Structure Title";
		var key_fieldName = "Geolocation";
		
		DataEngine.editFieldSearchable(
			fieldFieldLabel = "${key_fieldName}",
			searchableType = "Disable");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "${key_structureName}");

		DataEngine.viewFieldSearchable(
			fieldFieldLabel = "${key_fieldName}",
			searchableType = "Disable");
		
		DataEngine.editFieldSearchable(
			fieldFieldLabel = "${key_fieldName}",
			searchableType = "Keyword");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "${key_structureName}");

		DataEngine.viewFieldSearchable(
			fieldFieldLabel = "${key_fieldName}",
			searchableType = "Keyword");
		
		DataEngine.editFieldSearchable(
			fieldFieldLabel = "${key_fieldName}",
			searchableType = "Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "${key_structureName}");

		DataEngine.viewFieldSearchable(
			fieldFieldLabel = "${key_fieldName}",
			searchableType = "Text");
	}

	@description = "This is a test for LPS-98071. As a Developer, I want to Implement the geolocation field type"
	@priority = "4"
	test DuplicateGeolocation {
		var key_fieldName = "Geolocation";

		DataEngine.addField(
			fieldFieldLabel = "${key_fieldName}",
			fieldName = "${key_fieldName}");

		DataEngine.duplicate(fieldFieldLabel = "${key_fieldName}");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		AssertElementPresent(
			key_fieldsetLabel = "${key_fieldName}",
			locator1 = "FormFields#LABEL");

		AssertElementPresent(
			key_fieldsetLabel = "Copy of ${key_fieldName}",
			locator1 = "FormFields#LABEL");
	}

	@description = "This is a test for LPS-98071. As a Developer, I want to Implement the geolocation field type"
	@priority = "4"
	test DuplicateRepeatableGeolocationOnWebContent {
		property portal.acceptance = "false";

		DataEngine.editFieldRepeatable(fieldFieldLabel = "Geolocation");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		AssertElementPresent(locator1 = "FormFields#FIELD_ADD_DUPLICATE");

		WebContentStructures.saveCP();

		Navigator.gotoNavItem(navItem = "Web Content");

		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "WC Structure Title");

		WebContent.addCP(webContentTitle = "Web Content Title");

		DataEngine.addRepeatableField(fieldName = "Geolocation");

		Button.clickPublish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		AssertElementPresent(locator1 = "FormFields#FIELD_ADD_REMOVE");
	}

	@description = "This is a test for LPS-98071. As a Developer, I want to Implement the geolocation field type"
	@priority = "5"
	test SetGeolocationLabelHelpTextAndReference {
		var key_fieldName = "Geolocation";
		var key_structureName = "WC Structure Title";

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "${key_structureName}");

		FormFields.viewTextLabel(
			fieldLabel = "${key_fieldName}",
			fieldName = "${key_fieldName}");

		
		DataEngine.editFieldTip(
			fieldFieldLabel = "${key_fieldName}",
			fieldHelp = "Help Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "${key_structureName}");

		FormFields.viewFieldHelpText(
			fieldHelpText = "Help Text",
			fieldName = "${key_fieldName}");
		
		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "${key_structureName}e");

		AssertClick.assertPartialTextClickAt(
			key_fieldFieldLabel = "${key_fieldName}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER",
			value1 = "${key_fieldName}");

		if (IsElementNotPresent(locator1 = "NavTab#ACTIVE_TAB_LINK", key_tab = "Advanced")) {
			Navigator.gotoNavTab(navTab = "Advanced");
		}

		AssertTextEquals(
			locator1 = "Form#FIELD_REFERENCE",
			value1 = "${key_fieldName}");
	}

	@description = "This is a test for LPS-98071. As a Developer, I want to Implement the geolocation field type"
	@priority = "3"
	test ShowAndHideGeolocationLabel {
		var key_fieldName = "Geolocation";

		DataEngine.addField(
			fieldFieldLabel = "${key_fieldName}",
			fieldName = "${key_fieldName}");

		DataEngine.editShowOrHideLabel(
			fieldFieldLabel = "${key_fieldName}",
			showLabel = "true");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		AssertElementPresent(
			key_fieldFieldLabel = "${key_fieldName}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		DataEngine.editShowOrHideLabel(
			fieldFieldLabel = "${key_fieldName}",
			showLabel = "false");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		AssertElementNotPresent(
			key_fieldFieldLabel = "${key_fieldName}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");
	}

	@description = "This is a test for LPS-98071. As a Developer, I want to Implement the geolocation field type"
	@priority = "4"
	test VerifyDuplicatedGeolocationOnWebContent {
		var key_fieldName = "Geolocation";

		DataEngine.editFieldRepeatable(fieldFieldLabel = "${key_fieldName}");

		WebContentStructures.saveCP();

		Navigator.gotoNavTab(navTab = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Title");

		WebContent.addWithStructureCP(webContentTitle = "WC WebContent Title");

		DataEngine.addRepeatableField(fieldName = "${key_fieldName}");

		Button.clickPublish();

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		DataEngine.assertFieldRepeated(
			fieldName = "${key_fieldName}",
			index = "2");
	}

	@description = "This is a test for LPS-98071. As a Developer, I want to Implement the geolocation field type"
	@ignore = "true"
	@priority = "5"
	test VerifyRequiredPropertyOfGeolocation {
		var key_fieldName = "Geolocation";

		DataEngine.addField(
			fieldFieldLabel = "${key_fieldName}",
			fieldName = "${key_fieldName}");

		DataEngine.editFieldRequired(fieldFieldLabel = "${key_fieldName}");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		AssertElementPresent(locator1 = "Icon#ASTERISK");

		AssertClick.assertPartialTextClickAt(
			key_fieldFieldLabel = "${key_fieldName}",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER",
			value1 = "${key_fieldName}");

		Uncheck.uncheckToggleSwitch(
			key_toggleSwitchLabel = "Required Field",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		AssertElementNotPresent(locator1 = "Icon#ASTERISK");
	}

}