definition {

	macro clickToAddEventButton {
		var key_DefinitionsOptions = "event-analysis/create";

		if (IsElementPresent(locator1 = "ACSettings#DEFINITIONS_OPTIONS")) {
			Click(locator1 = "ACSettings#DEFINITIONS_OPTIONS");
		}

		Click(locator1 = "ACEventAnalysis#ADD_EVENT_BUTTON");
	}

	macro clickToAddBreakdownButton {
		var key_sectionType = "breakdown";

		Click(locator1 = "ACEventAnalysis#ADD_ATTRIBUTE_BUTTON");
	}

	macro clickToAddFilterButton {
		var key_sectionType = "filter";

		Click(locator1 = "ACEventAnalysis#ADD_ATTRIBUTE_BUTTON");
	}

	macro searchEventAnalysis {
		Type(
			locator1 = "ACEventAnalysis#SEARCH_BAR_EVENT_ANALYSE",
			value1 = "${searchItem}");
	}

	macro viewEventAnalysisList {
		for (var name : list "${eventList}") {
			var key_buttonName = "${name}";

			while (IsElementNotPresent(locator1 = "ACUtils#GENERIC_BUTTON") && (maxIterations = "50")) {
				ACEventAnalysis.searchEventAnalysis(searchItem = "${name}");
			}

			AssertTextEquals(
				locator1 = "ACUtils#GENERIC_BUTTON",
				value1 = "${name}");
		}
	}

	macro viewEventAnalysisListNotPresent {
		for (var name : list "${eventList}") {
			var key_buttonName = "${name}";

			AssertElementNotPresent(locator1 = "ACUtils#GENERIC_BUTTON");
		}
	}

	macro addEventToAnalysis {
		var key_buttonName = "${customEventName}";

		ACEventAnalysis.clickToAddEventButton();

		ACEventAnalysis.searchEventAnalysis(searchItem = "${customEventName}");

		while (IsElementNotPresent(locator1 = "ACUtils#GENERIC_BUTTON") && (maxIterations = "50")) {
			TestUtils.hardRefresh();

			ACEventAnalysis.clickToAddEventButton();

			ACEventAnalysis.searchEventAnalysis(searchItem = "${customEventName}");
		}

		Click(locator1 = "ACUtils#GENERIC_BUTTON");
	}

	macro changeAttributeDataType {
		var key_buttonName = "${attributeName}";

		MouseOver(locator1 = "ACUtils#GENERIC_BUTTON");

		if ("${editOption}" == "Description") {
			Click(locator1 = "ACEventAnalysis#EDIT_PENCIL");

			ACCustomEvents.changeAttributeDataType(newDataType = "${newDataType}");
		}

		if ("${editOption}" == "Filter") {
			Click(locator1 = "ACEventAnalysis#CONDITION_BUTTON");

			Click(locator1 = "ACEventAnalysis#EDIT_PENCIL");

			ACCustomEvents.changeAttributeDataType(newDataType = "${newDataType}");
		}
	}
}