@component-name = "portal-questions"
definition {

	property osgi.module.configuration.file.names = "com.liferay.questions.web.internal.configuration.QuestionsConfiguration.config";
	property osgi.module.configurations = "com.liferay.portlet.display-category=&quot;category.collaboration&quot;\nenableRedirectToLogin=&quot;true&quot;";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Questions";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Questions Page");

		Navigator.gotoPage(pageName = "Questions Page");

		Portlet.addPG(portletName = "Questions");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.configurePage(
			columnName = "1 Column",
			pageName = "Questions Page",
			privacy = "Public Pages",
			sitePageType = "Widget Page");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			ProductMenu.gotoPortlet(
				category = "Content &amp; Data",
				portlet = "Message Boards");

			if (IsElementPresent(locator1 = "Questions#QUESTIONS_LIST")) {
				Questions.tearDown();
			}

			Page.tearDownCP();
		}
	}

	@description = "Validate if the user can post a question with non-Latin characters， it also covers LPS-128639."
	@priority = "5"
	test AddQuestionWithNonLatinCharacters {
		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoTab(questionTabField = "Questions");

		Questions.addRootTopic(
			rootTopicDescription = "トピック記述",
			rootTopicName = "質問トピック");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピック");

		Questions.askQuestionFillingTheMandatoryFields(
			contentField = "私の好きな日本料理は寿司とすき焼きです",
			titleField = "あなたの好きな日本料理は何ですか");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピック");

		Questions.gotoDetails(questionTitle = "あなたの好きな日本料理は何ですか");
	}

	@description = "Validate if the user can add a comment to an answer with non-Latin characters."
	@priority = "4"
	test ReplyToAnAnswerWithNonLatinCharacters {
		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoTab(questionTabField = "Questions");

		Questions.addRootTopic(
			rootTopicDescription = "トピック記述",
			rootTopicName = "質問トピック");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピック");

		Questions.askQuestionFillingTheMandatoryFields(
			contentField = "あなたの好きな日本料理は寿司とすき焼きですか",
			titleField = "あなたの好きな日本料理は何ですか");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピック");

		Questions.gotoDetails(
			questionTabField = "Questions",
			questionTitle = "あなたの好きな日本料理は何ですか");

		Questions.answerQuestion(contentField = "私の好きな日本料理はフグ刺しと天ぷらです");

		Questions.replyToComment(commentReply = "私の好きな日本料理は焼肉と鮭の刺身です");
	}

	@description = "Validate if the user can view a question via an edited topic that contains non-Latin characters."
	@priority = "4"
	test ViewQuestionViaEditedTopic {
		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoTab(questionTabField = "Questions");

		Questions.addRootTopic(
			rootTopicDescription = "トピック記述",
			rootTopicName = "質問トピック");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピック");

		Questions.askQuestionFillingTheMandatoryFields(
			contentField = "あなたの好きな日本料理は寿司とすき焼きですか",
			titleField = "あなたの好きな日本料理は何ですか");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		Questions.editRootTopic(
			categoryName = "質問トピック",
			categoryNameEdit = "質問トピックを編集する");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピックを編集する");

		Questions.gotoDetails(questionTitle = "あなたの好きな日本料理は何ですか");
	}

	@description = "Validate if the user can redirect to the question after clicking a tag that contains non-Latin characters."
	@priority = "5"
	test ViewQuestionViaTags {
		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoTab(questionTabField = "Questions");

		Questions.addRootTopic(
			rootTopicDescription = "トピック記述",
			rootTopicName = "質問トピック");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピック");

		Questions.askQuestionFillingTheMandatoryFields(
			contentField = "私の好きな日本料理は寿司とすき焼きです",
			tagLocatorList = "おおさか,とうきょう",
			titleField = "あなたの好きな日本料理は何ですか");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoTab(questionTabField = "Tags");

		for (var tagName : list "おおさか,とうきょう") {
			AssertElementPresent(
				key_tagName = "${tagName}",
				locator1 = "Questions#TAGS_LIST_TAB_NAMES");
		}

		Questions.search(
			searchKey = "おおさか",
			searchTag = "true");

		Questions.viewTags(tagName = "おおさか");

		Questions.viewNoTags(tagsList = "とうきょう");

		Click(
			key_tagName = "おおさか",
			locator1 = "Questions#TAGS_LIST_TAB_NAMES");

		Questions.gotoDetails(questionTitle = "あなたの好きな日本料理は何ですか");
	}

	@description = "Validate if the user can view the sorted tags that contain non-Latin characters."
	@priority = "4"
	test ViewSortedTags {
		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoTab(questionTabField = "Questions");

		Questions.addRootTopic(
			rootTopicDescription = "トピック記述",
			rootTopicName = "質問トピック");

		for (var num : list "1,2") {
			Navigator.gotoPage(pageName = "Questions Page");

			Questions.gotoCategory(categoryName = "質問トピック");

			Questions.askQuestionFillingTheMandatoryFields(
				contentField = "Question Body ${num}",
				tagLocatorList = "おおさか",
				titleField = "Question ${num}");
		}

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピック");

		Questions.askQuestionFillingTheMandatoryFields(
			contentField = "Question Body 3",
			tagLocatorList = "とうきょう",
			titleField = "Question 3");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "質問トピック");

		Questions.gotoTab(questionTabField = "Tags");

		Questions.viewTagsFilterResults(
			position = "1",
			tagName = "おおさか",
			usedTimes = "Used 2 Times");

		Questions.viewTagsFilterResults(
			position = "2",
			tagName = "とうきょう",
			usedTimes = "Used 1 Times");
	}

}