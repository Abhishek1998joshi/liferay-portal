@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Site Administration";
	property testray.main.component.name = "Site Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();
		}
	}

	@description = "Inactive and reactive site."
	@priority = "4"
	test ActivateSite {
		property test.name.skip.portal.instance = "Sites#ActivateSite";

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Infrastructure",
			configurationName = "Inactive Request Handler",
			configurationScope = "System Scope");

		FormFields.enableCheckbox(fieldName = "Show Inactive Request Message");

		SystemSettings.saveConfiguration();

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		Site.openSiteSettingsAdmin(siteURLKey = "site-name");

		Site.inactivateCP(siteName = "Site Name");

		var baseURL = PropsUtil.get("portal.url");

		Navigator.openSpecificURL(url = "${baseURL}/web/site-name");

		Page.viewInactiveRequestMessage(resource = "site");

		Site.openSitesAdmin();

		Site.activateInactive(siteName = "Site Name");

		Navigator.openSiteURL(siteName = "Site Name");

		AssertTextNotPresent(value1 = "This site is inactive. Please contact the administrator.");
	}

	@description = "Add child site in child site."
	@priority = "4"
	test AddChildChildSite {
		JSONGroup.addGroup(groupName = "Site Name");

		JSONGroup.addChildGroup(
			groupName = "Child SiteName",
			parentGroupName = "Site Name");

		JSONGroup.addChildGroup(
			grandParentGroupName = "Site Name",
			groupName = "Child Child SiteName",
			parentGroupName = "Child SiteName");

		Site.openSitesAdmin();

		Site.viewChildCP(
			childSiteName = "Child SiteName",
			siteName = "Site Name");

		Site.viewChildCP(
			childSiteName = "Child Child SiteName",
			siteName = "Child SiteName");
	}

	@description = "View public page via virtual host URL."
	@priority = "3"
	test AddPublicVirtualHostsSiteURL {
		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		Site.openSiteSettingsAdmin(siteURLKey = "site-name");

		Site.addVirtualHostsURLCP(
			publicPageVirtualHostURL = "www.able.com",
			publicVirtualHosts = "true");

		Navigator.openSpecificURL(url = "http://www.able.com:8080/site-page");

		Site.viewVirtualHostsURLPG(
			pageName = "Site Page",
			siteName = "Site Name",
			sitePageURL = "http://www.able.com:8080/site-page");
	}

	@description = "Able to add a site with comma."
	@priority = "3"
	test AddSiteWithCommaInName {
		JSONGroup.addGroup(groupName = "Site Name,");

		Site.openSitesAdmin();

		Site.viewCP(siteName = "Site Name,");
	}

	@description = "Unable to add sanme site name."
	@priority = "3"
	test AddSiteWithInvalidDuplicateName {
		JSONGroup.addGroup(groupName = "Site Name");

		Site.openSitesAdmin();

		Site.viewCannotAdd(
			siteName = "Site Name",
			siteNameInvalid = "Duplicate");
	}

	@description = "Unable to add site name with invalid characters * and numeric."
	@priority = "3"
	test AddSiteWithInvalidName {
		for (var siteName : list " ,Site Name*,22222") {
			Site.openSitesAdmin();

			Site.viewCannotAdd(
				siteName = "${siteName}",
				siteNameInvalid = "Invalid");
		}
	}

	@description = "Deleting firiendly URL makes it not able to access page via fridely URL."
	@priority = "4"
	test DeleteSiteFriendlyURL {
		Site.openSitesAdmin();

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Public Page");

		JSONLayout.addPrivateLayout(
			groupName = "Site Name",
			layoutName = "Private Page");

		Site.openSiteSettingsAdmin(siteURLKey = "site-name");

		Site.editFriendlyURLCP(siteFriendlyURLEdit = "");

		var siteID = JSONGroupAPI._getGroupIdByName(groupName = "Site Name", site = "true");

		AssertTextEquals(
			locator1 = "TextInput#FRIENDLY_URL_SITE_SETTINGS",
			value1 = "/group-${siteID}");

		Navigator.openSiteURL(siteName = "site-name");

		Page.viewNoPG(pageName = "Public Page");

		AssertTextEquals(
			locator1 = "Home#ERROR_MESSAGE_ALERT_ERROR",
			value1 = "Not Found");

		Navigator.openPrivateSiteURL(siteName = "site-name");

		Page.viewNoPG(pageName = "Private Page");

		AssertTextEquals(
			locator1 = "Home#ERROR_MESSAGE_ALERT_ERROR",
			value1 = "Not Found");
	}

	@description = "Could edit site name."
	@priority = "5"
	test EditSite {
		property portal.acceptance = "true";

		JSONGroup.addGroup(groupName = "Site Name");

		Site.openSiteSettingsAdmin(siteURLKey = "site-name");

		Site.editCP(
			siteName = "Site Name",
			siteNameEdit = "Site Name Edit");

		Site.openSitesAdmin();

		Site.viewCP(siteName = "Site Name Edit");
	}

	@description = "Could edit friendly URL and access by it."
	@priority = "4"
	test EditSiteFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		Site.openSitesAdmin();

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		JSONLayout.addPrivateLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		Site.openSiteSettingsAdmin(siteURLKey = "site-name");

		Site.editFriendlyURLCP(siteFriendlyURLEdit = "alpha");

		Navigator.openSiteURL(siteName = "alpha");

		Site.viewSiteFriendlyURLPG(
			pageName = "Public Page",
			siteName = "Site Name",
			sitePageURL = "${portalURL}/web/alpha");

		Navigator.openPrivateSiteURL(siteName = "alpha");

		Site.viewSiteFriendlyURLPG(
			pageName = "Private Page",
			siteName = "Site Name",
			sitePageURL = "${portalURL}/group/alpha");
	}

	@description = "Could not add invalid friendly URL and can access by former friendly URL."
	@priority = "3"
	test EditSiteFriendlyURLInvalid {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "Sites#EditSiteFriendlyURLInvalid";

		var portalURL = PropsUtil.get("portal.url");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		Site.openSiteSettingsAdmin(siteURLKey = "site-name");

		Site.editFriendlyURLCP(
			friendlyURLInvalid = "true",
			siteFriendlyURL = "/site-name",
			siteFriendlyURLEdit = "/alpha/alpha");

		SitePages.openPagesAdmin(siteURLKey = "site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Site Page");

		Page.viewFriendlyURL(
			pageName = "Site Page",
			siteFriendlyURL = "${portalURL}/web/site-name");
	}

	@description = "Able to search and find site by site name."
	@priority = "5"
	test SearchSite {
		property portal.acceptance = "true";

		JSONGroup.addGroup(groupName = "Site Name");

		Site.openSitesAdmin();

		Site.searchCP(siteName = "Site Name");
	}

	@description = "Can not choose its own site as parent site."
	@priority = "3"
	test SiteCannotBeItsOwnParent {
		JSONGroup.addGroup(groupName = "Site Name");

		Site.openSiteSettingsAdmin(siteURLKey = "site-name");

		Site.selectParentSite(
			chooseButtonAvailable = "false",
			parentSiteName = "Site Name");
	}

	@description = "Can not choose its own site as parent site in staging."
	@priority = "3"
	test SiteCannotBeItsOwnParentStaging {
		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		JSONStaging.enableLocalStaging(groupName = "Site Name");

		Navigator.openSiteURL(siteName = "Site Name");

		Navigator.gotoStagedView();

		Staging.viewStagingLocalLivePG();

		Site.openSiteSettingsAdmin(siteURLKey = "site-name");

		Site.selectParentSite(
			chooseButtonAvailable = "false",
			parentSiteName = "Site Name");
	}

	@description = "View Web Content Admin displayed in global site."
	@priority = "4"
	test ViewDefaultGlobalScopeLandingPage {
		property testray.component.names = "Site Administration";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "global");

		ProductMenuHelper.openProductMenu();

		Site.viewGlobalLandingDefault(
			portletName = "Web Content",
			siteName = "Global");
	}

	@description = "View parent site in child site's Sites Directory."
	@priority = "4"
	test ViewDisplayParentSiteViaSitesDirectory {
		Site.openSitesAdmin();

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		JSONGroup.addChildGroup(
			groupName = "Child SiteName",
			parentGroupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Child SiteName",
			layoutName = "Site Page",
			parentGroupName = "Site Name");

		JSONGroup.addChildGroup(
			grandParentGroupName = "Site Name",
			groupName = "Child ChildSiteName",
			parentGroupName = "Child SiteName");

		JSONLayout.addPublicLayout(
			grandParentGroupName = "Site Name",
			groupName = "Child ChildSiteName",
			layoutName = "Site Page",
			parentGroupName = "Child SiteName");

		Site.openSitesAdmin();

		Site.viewChildCP(
			childSiteName = "Child SiteName",
			siteName = "Site Name");

		Site.viewChildCP(
			childSiteName = "Child ChildSiteName",
			siteName = "Child SiteName");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Child SiteName",
			layoutName = "Site Page",
			parentGroupName = "Site Name",
			widgetName = "Sites Directory");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child SiteName");

		SitesDirectory.configureSetupPG(directorySites = "Parent Level");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child SiteName");

		SitesDirectory.viewDescriptivePG(siteName = "Site Name");
	}

	@description = "View sites in child site's Sites Directory."
	@priority = "4"
	test ViewSiteHierarchiesViaSitesDirectory {
		Site.openSitesAdmin();

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		JSONGroup.addChildGroup(
			groupName = "Child Site1Name",
			parentGroupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Child Site1Name",
			layoutName = "Site Page",
			parentGroupName = "Site Name");

		JSONGroup.addChildGroup(
			grandParentGroupName = "Site Name",
			groupName = "Child ChildSite1Name",
			parentGroupName = "Child Site1Name");

		JSONLayout.addPublicLayout(
			grandParentGroupName = "Site Name",
			groupName = "Child ChildSite1Name",
			layoutName = "Site Page",
			parentGroupName = "Child Site1Name");

		JSONGroup.addChildGroup(
			grandParentGroupName = "Site Name",
			groupName = "Child ChildSite2Name",
			parentGroupName = "Child Site1Name");

		JSONLayout.addPublicLayout(
			grandParentGroupName = "Site Name",
			groupName = "Child ChildSite2Name",
			layoutName = "Site Page",
			parentGroupName = "Child Site1Name");

		JSONGroup.addChildGroup(
			groupName = "Child Site2Name",
			parentGroupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Child Site2Name",
			layoutName = "Site Page",
			parentGroupName = "Site Name");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Child Site1Name",
			layoutName = "Site Page",
			parentGroupName = "Site Name",
			widgetName = "Sites Directory");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child Site1Name");

		SitesDirectory.configureSetupPG(directorySites = "Top Level");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child Site1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Site Name");

		SitesDirectory.configureSetupPG(directorySites = "Parent Level");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child Site1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Site Name");

		SitesDirectory.configureSetupPG(directorySites = "Siblings");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child Site1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Child Site1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Child Site2Name");

		SitesDirectory.configureSetupPG(directorySites = "Children");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child Site1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Child ChildSite1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Child ChildSite2Name");

		JSONLayout.addWidgetToPublicLayout(
			grandParentGroupName = "Site Name",
			groupName = "Child ChildSite1Name",
			layoutName = "Site Page",
			parentGroupName = "Child Site1Name",
			widgetName = "Sites Directory");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child ChildSite1Name");

		SitesDirectory.configureSetupPG(directorySites = "Top Level");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child ChildSite1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Site Name");

		SitesDirectory.configureSetupPG(directorySites = "Parent Level");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child ChildSite1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Child Site1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Child Site2Name");

		SitesDirectory.configureSetupPG(directorySites = "Siblings");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child ChildSite1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Child ChildSite1Name");

		SitesDirectory.viewDescriptivePG(siteName = "Child ChildSite2Name");

		SitesDirectory.configureSetupPG(directorySites = "Children");

		Navigator.gotoSitePage(
			pageName = "Site Page",
			siteName = "Child ChildSite1Name");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No sites were found.");
	}

}