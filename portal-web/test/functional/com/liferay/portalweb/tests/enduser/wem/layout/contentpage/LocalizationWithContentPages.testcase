@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Content Page Name",
				type = "content");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-106776. Add a translation to inline content."
	@priority = "5"
	test AddTranslationToInlineContent {
		property portal.acceptance = "true";

		task ("Add a Paragraph fragment to the content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Paragraph");
		}

		task ("Edit the inline content of Paragraph") {
			PageEditor.editFragmentText(
				fragmentName = "Paragraph",
				id = "element-text",
				text = "New editable fragment en-US text");
		}

		task ("Add a translation to inline content") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "ca-ES");

			PageEditor.editFragmentText(
				fragmentName = "Paragraph",
				id = "element-text",
				text = "New editable fragment ca-ES text");

			PageEditor.clickPublish();
		}

		task ("Navigate to the content page in Edit mode") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");
		}

		task ("View inline content in en-US") {
			PageEditor.viewFragmentEditableText(
				editableText = "New editable fragment en-US text",
				fragmentName = "Paragraph",
				id = "element-text");
		}

		task ("View localized inline content") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "ca-ES");

			PageEditor.viewFragmentEditableText(
				editableText = "New editable fragment ca-ES text",
				fragmentName = "Paragraph",
				id = "element-text");
		}
	}

	@description = "This is a test for LPS-122148. Search localized inline text items in Page Content panel."
	@priority = "4"
	test SearchLocalizedInlineTextItemsInPageContentPanel {
		task ("Add a Heading fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Add a Banner Center below the Heading") {
			PageEditor.addFragment(
				collectionName = "Featured Content",
				fragmentName = "Banner Center",
				targetFragmentName = "Heading");
		}

		task ("Edit the inline text of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "Edited Text");
		}

		task ("Edit the inline text of title field in Banner Center") {
			PageEditor.editFragmentText(
				fragmentName = "Banner Center",
				id = "01-title",
				text = "Edited Title");
		}

		task ("Edit the inline text of subtitle field in Banner Center") {
			PageEditor.editFragmentText(
				fragmentName = "Banner Center",
				id = "02-subtitle",
				text = "Edited Subtitle");
		}

		task ("Change locale to es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("Edit the localized inline text of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "Texto editado");
		}

		task ("Edit the localized inline text of title field in Banner Center") {
			PageEditor.editFragmentText(
				fragmentName = "Banner Center",
				id = "01-title",
				text = "Título editado");
		}

		task ("Change locale to en-US") {
			PageEditor.changeLocale(
				currentLocale = "es-ES",
				locale = "en-US");
		}

		task ("View the inline texts in Page Content tab") {
			PageEditor.gotoTab(tabName = "Browser");

			Navigator.gotoNavTab(navTab = "Page Content");

			for (var itemName : list "Edited Text,Edited Title,Edited Subtitle") {
				AssertVisible(
					key_itemName = "${itemName}",
					locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
			}
		}

		task ("Change locale to es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("View the inline texts in Page Content tab") {
			for (var itemName : list "Texto editado,Título editado,Edited Subtitle") {
				AssertVisible(
					key_itemName = "${itemName}",
					locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
			}
		}

		task ("Search the localized inline text in Page Content panel") {
			PageEditor.searchInPageContentPanel(searchTerm = "Texto");

			AssertVisible(
				key_itemName = "Texto editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			for (var itemName : list "Título editado,Edited Subtitle") {
				AssertElementNotPresent(
					key_itemName = "${itemName}",
					locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
			}

			PageEditor.searchInPageContentPanel(searchTerm = "Título");

			AssertVisible(
				key_itemName = "Título editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			for (var itemName : list "Texto editado,Edited Subtitle") {
				AssertElementNotPresent(
					key_itemName = "${itemName}",
					locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
			}
		}
	}

	@description = "This is a test for LPS-122148. Only the localized inline text items from current experience shown in Page Content panel."
	@priority = "4"
	test ViewLocalizedInlineTextItemsFromCurrentExperienceInPageContentPanel {
		task ("Add a new experience with Spanish") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addExperienceWithLanguage(
				experienceName = "New Experience",
				language = "Spanish",
				segmentName = "Anyone");

			PageEditor.clickManagementBar();
		}

		task ("Switch to the default experience") {
			PageEditor.changeExperience(newExperience = "Default");
		}

		task ("Add a Heading fragment to page") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Change locale to es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("Edit the localized inline text of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "Texto editado");
		}

		task ("Switch to the new experience and en-US") {
			PageEditor.changeExperienceLanguage(
				language = "en-US",
				newExperience = "New Experience");
		}

		task ("Add a Paragraph to page") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Paragraph");
		}

		task ("Change locale to es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("Edit the localized inline text of Paragraph") {
			PageEditor.editFragmentText(
				fragmentName = "Paragraph",
				id = "element-text",
				text = "Texto enriquecido editado");
		}

		task ("Switch to the default experience and en-US") {
			PageEditor.changeExperienceLanguage(
				language = "en-US",
				newExperience = "Default");
		}

		task ("View only the default inline text of Heading shown in Page Content tab") {
			PageEditor.gotoTab(tabName = "Browser");

			Navigator.gotoNavTab(navTab = "Page Content");

			AssertVisible(
				key_itemName = "Heading Example",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			AssertElementNotPresent(
				key_itemName = "Texto editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			AssertElementNotPresent(
				key_itemName = "A paragraph is a self-contained unit of a discourse in writing dealing with a particular point or idea. Paragraphs are usually an expected part of formal writing, used to organize longer prose.",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
		}

		task ("Change locale to es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("View only the localized inline text of Heading shown in Page Content tab") {
			AssertVisible(
				key_itemName = "Texto editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			AssertElementNotPresent(
				key_itemName = "Heading Example",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			AssertElementNotPresent(
				key_itemName = "A paragraph is a self-contained unit of a discourse in writing dealing with a particular point or idea. Paragraphs are usually an expected part of formal writing, used to organize longer prose.",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
		}

		task ("Switch to the new experience and en-US") {
			PageEditor.changeExperienceLanguage(
				language = "en-US",
				newExperience = "New Experience");
		}

		task ("View only the default inline text of Paragraph shown in Page Content tab") {
			PageEditor.gotoTab(tabName = "Browser");

			Navigator.gotoNavTab(navTab = "Page Content");

			AssertVisible(
				key_itemName = "A paragraph is a self-contained unit of a discourse in writing dealing with a particular point or idea. Paragraphs are usually an expected part of formal writing, used to organize longer prose.",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			AssertElementNotPresent(
				key_itemName = "Texto enriquecido editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			AssertElementNotPresent(
				key_itemName = "Heading Example",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
		}

		task ("Change locale to es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("View only the localized inline text of Paragraph shown in Page Content tab") {
			AssertVisible(
				key_itemName = "Texto enriquecido editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			AssertElementNotPresent(
				key_itemName = "A paragraph is a self-contained unit of a discourse in writing dealing with a particular point or idea. Paragraphs are usually an expected part of formal writing, used to organize longer prose.",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			AssertElementNotPresent(
				key_itemName = "Texto editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
		}
	}

}