@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Asset Publisher";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Asset Publisher Page");
		}

		task ("Add an Asset Publisher widget to page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Asset Publisher Page",
				widgetName = "Asset Publisher");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test");

			Site.tearDownCP();

			JSONUser.tearDownNonAdminUsers();
		}
	}

	@description = "This is a test for LPS-124704. No alert popup on view asset in Asset Publisher when replace the value of assetEntryId by XSS in URL."
	@priority = "4"
	@uitest
	test ViewAssetDetailsWhenAppendXSSToURL {
		task ("Add three web contents") {
			for (var i : list "1,2,3") {
				JSONWebcontent.addWebContent(
					content = "WC WebContent Content",
					groupName = "Test Site Name",
					title = "WC WebContent Title ${i}");
			}
		}

		task ("Add a page with an Asset Publisher widget and display the Web Contents article in the Asset Publisher Widget") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Asset Publisher Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Asset Publisher Page",
				widgetName = "Asset Publisher");

			Navigator.gotoSitePage(
				pageName = "Asset Publisher Page",
				siteName = "Test Site Name");
		}

		task ("View the first web content details via Asset Publisher") {
			AssetPublisherPortlet.clickAssetTitle(assetTitle = "WC WebContent Title ${i}");
		}

		task ("Click the back button of Asset Publisher") {
			PageEditor.clickBack();
		}

		task ("Replace the value of assetEntryId by XSS in URL") {
			var portalURL = PropsUtil.get("portal.url");

			var assetEntryId = '''
				${portalURL}/web/test-site-name/asset-publisher-page?assetEntryId=</script> <script> alert (1) </script>
				''';

			Navigator.openSpecificURL(url = "${assetEntryId}");
		}

		task ("View the alert not shown") {
			AssertAlertNotPresent();
		}
	}

}