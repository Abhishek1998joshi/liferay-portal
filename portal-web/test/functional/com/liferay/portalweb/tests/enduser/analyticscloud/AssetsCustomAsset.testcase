@component-name = "portal-analytics-cloud"
definition {

	property analytics.cloud.enabled = "true";
	property minimum.slave.ram = "32";
	property portal.release = "false";
	property portal.upstream = "true";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		ACUtils.launchAC();

		ACSettings.takeTokenFromAnalyticsCloud();

		TestCase.setUpPortalInstance();

		ACUtils.launchDXP();

		ACUtils.addSiteAndPage();

		ACUtils.createWCAndAddToPage(
			webContentRichText = '''<div data-analytics-asset-type='custom' data-analytics-asset-id='analytics-portal' \
			data-analytics-asset-category='AC' data-analytics-asset-title='Custom Asset Analytics Cloud'> \
			<h1> What's your favorite portal? </h1> </div>''',
			webContentTitle = "Custom Asset AC Title");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ACUtils.tearDownDXP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}

		ACUtils.tearDownAC();
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8132 | Test Summary: Custom asset list shows a list of custom assets"
	@priority = "5"
	test CanShowsCustomAssetList {
		for (var layoutName : list "AC Page 2,AC Page 3") {
			ACUtils.addSiteAndPage(layoutName = "${layoutName}");
		}

		for (var n : list "2,3") {
			ACUtils.createWCAndAddToPage(
				layoutName = "AC Page ${n}",
				webContentRichText = '''<div data-analytics-asset-type='custom' data-analytics-asset-id='analytics-portal-${n}' \
				data-analytics-asset-category='AC' data-analytics-asset-title='Custom Asset Analytics Cloud ${n}'> \
				<h1> What's your favorite portal ${n}? </h1> </div>''',
				webContentTitle = "Custom Asset AC Title ${n}");
		}

		var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");

		JSONUser.addUser(
			userEmailAddress = "ac@liferay.com",
			userFirstName = "ac",
			userLastName = "ac",
			userScreenName = "ac");

		ACDXPSettings.syncNewContactData();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "ac@liferay.com",
			userLoginFullName = "ac ac");

		for (var pageName : list "AC Page,AC Page 2,AC Page 3") {
			Navigator.gotoSitePage(
				pageName = "${pageName}",
				siteName = "Site Name");
		}

		ACUtils.closeAllSessions();

		Pause(locator1 = "30000");

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = "${propertyName}");

		ACNavigation.goToAssets();

		ACNavigation.switchTab(tabName = "Custom");

		for (var assetName : list "Custom Asset Analytics Cloud,Custom Asset Analytics Cloud 2,Custom Asset Analytics Cloud 3") {
			ACAssets.viewAssets(assetName = "${assetName}");
		}
	}

}