@component-name = "portal-analytics-cloud"
definition {

	property analytics.cloud.enabled = "true";
	property minimum.slave.ram = "24";
	property portal.release = "false";
	property portal.upstream = "true";
	property slave.label = "slave-1";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		task ("Copy the token from AC") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();
		}

		task ("Set up instance and sign in DXP") {
			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();
		}

		task ("Add a new site with a public widget page") {
			ACUtils.addSiteAndPage();
		}

		task ("Connect the DXP to AC") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ACUtils.tearDownDXP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}

		ACUtils.tearDownAC();
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8330 | Test Summary: Assert Chart ticks for pages Visitor Behavior standardized"
	@ignore = "true"
	@priority = "5"
	test AssertChartTicksPagesVisitorBehaviorStandardized {

		// TODO LRAC-8330 AssertChartTicksPagesVisitorBehaviorStandardized

	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8329 | Test Summary: Assert Chart ticks for Site Metrics standardized"
	@priority = "5"
	test AssertChartTicksSiteMetricsStandardized {
		task ("Get site assigned property name") {
			var propertyName = ACDXPSettings.getAssignedPropertyName(siteName = "Site Name");
		}

		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${propertyName}");
		}

		task ("Navigate to the Overview tab on Sites dashboard") {
			ACNavigation.goToSites();

			ACNavigation.switchTab(tabName = "Overview");
		}

		task ("View the time period is Last 30 days in site metrics chart") {
			ACTimeFilter.assertCurrentTimeFilterRange(
				cardName = "${propertyName} Activities",
				timeFilterRange = "Last 30 days");
		}

		task ("View the interval is D in site metrics chart") {
			ACCards.viewSelectedInterval(
				cardName = "${propertyName} Activities",
				interval = "D");
		}

		task ("View the format of recharts xAxis ticks is MMM d") {
			var date = ACCards.getAxisTick(
				axis = "xAxis",
				cardName = "${propertyName} Activities");

			ACUtils.assertDatePattern(
				date = "${date}",
				format = "MMM d");
		}

		task ("Select the W in interval selector") {
			ACCards.selectInterval(
				cardName = "${propertyName} Activities",
				interval = "W");
		}

		task ("View the format of recharts xAxis ticks is MMM d - d") {
			var date = ACCards.getAxisTick(
				axis = "xAxis",
				cardName = "${propertyName} Activities");

			ACUtils.assertDatePattern(
				date = "${date}",
				format = "MMM d - d");
		}

		task ("Select the M in interval selector") {
			ACCards.selectInterval(
				cardName = "${propertyName} Activities",
				interval = "M");
		}

		task ("View the format of recharts xAxis ticks is MMM") {
			var date = ACCards.getAxisTick(
				axis = "xAxis",
				cardName = "${propertyName} Activities");

			ACUtils.assertDatePattern(
				date = "${date}",
				format = "MMM");
		}

		task ("Select the Last 24 hours time period in site metrics chart") {
			ACTimeFilter.clickTimeFilterButton(cardName = "${propertyName} Activities");

			ACTimeFilter.setLast24Hours();
		}

		task ("View the interval option buttons are disabled") {
			for (var interval : list "D,W,M") {
				AssertVisible(
					key_text = "${interval}",
					locator1 = "Button#ANY_DISABLED");
			}
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8331 | Test Summary: Assert Site Metrics select Sessions/Visitor card tab"
	@priority = "5"
	test AssertSiteMetricsSelectSessionsPerVisitorCardTab {
		task ("Get site assigned property name") {
			var propertyName = ACDXPSettings.getAssignedPropertyName(siteName = "Site Name");
		}

		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${propertyName}");
		}

		task ("Navigate to the Overview tab on Sites dashboard") {
			ACNavigation.goToSites();

			ACNavigation.switchTab(tabName = "Overview");
		}

		task ("Navigate to the Sessions/Visitor tab") {
			ACNavigation.switchTabInCard(tabName = "Sessions/Visitor");
		}

		task ("View the Sessions/Visitor tab is active") {
			ACNavigation.viewActiveSwitchTabInCard(tabName = "Sessions/Visitor");
		}
	}

}