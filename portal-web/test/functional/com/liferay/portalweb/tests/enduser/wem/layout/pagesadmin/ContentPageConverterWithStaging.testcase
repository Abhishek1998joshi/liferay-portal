@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Site Pages Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		Staging.openStagingAdmin(siteURLKey = "test-site-name");

		Staging.activateStagingCP(siteName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-98327. When a widget page is converted to a content page and the content page is published, the widget page is replaced by the content page on live with local live staging."
	@priority = "2"
	test ViewContentPageInLiveAfterConvertingWidgetPageWithPublish {
		task ("Add a widget page in staging site") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name-staging",
				layoutName = "Test Page Name",
				site = "false");
		}

		task ("Add a web content based on Basic Web Content") {
			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				groupName = "Test Site Name (Staging)",
				site = "false",
				title = "Web Content Title");
		}

		task ("Add an Asset Publisher to widget page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name-staging",
				layoutName = "Test Page Name",
				site = "false",
				widgetName = "Asset Publisher");
		}

		task ("Assert the web content is shown in Asset Publisher") {
			Navigator.gotoStagedSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Portlet.viewTitle(portletName = "Asset Publisher");

			AssetPublisherPortlet.viewAssetPG(
				assetContent = "Web Content Content",
				assetTitle = "Web Content Title");
		}

		task ("Publish to Live") {
			Staging.gotoPublishToLive();

			Staging.publishToLive();
		}

		task ("Assert the page type is Widget Page in Live site") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			AssertVisible(
				key_pageName = "Test Page Name",
				key_pageType = "Widget Page",
				locator1 = "SitePages#LIST_GROUP_ITEM_PAGE_TYPE");
		}

		task ("Convert the widget page to content page") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name-staging");

			SitePages.gotoPageMenuItem(
				menuItem = "Convert to content page...",
				pageName = "Test Page Name");

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.clickPublish();

			AssertVisible(
				key_pageName = "Test Page Name",
				key_pageType = "Content Page",
				locator1 = "SitePages#LIST_GROUP_ITEM_PAGE_TYPE");
		}

		task ("Publish to Live") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name Staging");

			Staging.gotoPublishToLive();

			Staging.publishToLive();
		}

		task ("Assert the Asset Publisher is shown in Live site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Portlet.viewTitle(portletName = "Asset Publisher");

			AssetPublisherPortlet.viewAssetPG(
				assetContent = "Web Content Content",
				assetTitle = "Web Content Title");
		}

		task ("Assert the page type is Content Page in Live site") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			AssertVisible(
				key_pageName = "Test Page Name",
				key_pageType = "Content Page",
				locator1 = "SitePages#LIST_GROUP_ITEM_PAGE_TYPE");
		}
	}

	@description = "This is a test for LPS-98327. When a widget page is converted to a content page and the content page is not yet published, and the site is published the widget page is still visible on live with local live staging."
	@ignore = "true"
	@priority = "2"
	test ViewWidgetPageInLiveAfterConvertingWidgetPage {
		property portal.acceptance = "false";

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "Test Page Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.gotoPageMenuItem(
			menuItem = "Convert to content page...",
			pageName = "Test Page Name");

		Navigator.gotoSitePage(
			pageName = "Test Page Name",
			siteName = "Test Site Name Staging");

		Portlet.addPG(portletName = "Asset Publisher");

		AssertTextEquals(
			key_portletTitleName = "Asset Publisher",
			locator1 = "Home#PAGE_COLUMN_PORTLET_COLUMN_1_PORTLET_TITLE",
			value1 = "Asset Publisher");

		ProductMenu.gotoLiveSite(site = "Test Site Name");

		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandCategory(category = "Design");

		AssertElementNotPresent(
			key_portlet = "Fragments",
			locator1 = "ProductMenu#PORTLET");

	}

}