@component-name = "portal-document-management"
definition {

	property custom.properties = "default.layout.template.id=1_column";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Document Management";

	var pageName = "Documents and Media Page";
	var portletName = "Documents and Media";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		LexiconEntry.changeDisplayStyle(displayStyle = "list");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			DMDocumentType.tearDownCP();

			Page.tearDownCP();
		}
	}

	@priority = "5"
	@refactorneeded
	test AddDocuments {
		property testray.component.names = "Training";

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "Document_1.doc");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.doc",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.docx",
			dmDocumentTitle = "Document_1.docx");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.docx",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.jpeg",
			dmDocumentTitle = "Document_1.jpeg");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFileType = "Image",
			dmDocumentTitle = "Document_1.jpeg",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.jpg",
			dmDocumentTitle = "Document_1.jpg");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFileType = "Image",
			dmDocumentTitle = "Document_1.jpg",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Generated_by_adobe.jpg",
			dmDocumentTitle = "Generated_by_adobe.jpg");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFileType = "Image",
			dmDocumentTitle = "Generated_by_adobe.jpg",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_2.mp3",
			dmDocumentTitle = "Document_2.mp3");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_2.mp3",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.odt",
			dmDocumentTitle = "Document_1.odt");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.odt",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.pdf",
			dmDocumentTitle = "Document_1.pdf");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.pdf",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.png",
			dmDocumentTitle = "Document_1.png");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFileType = "Image",
			dmDocumentTitle = "Document_1.png",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.xls",
			dmDocumentTitle = "Document_1.xls");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.xls",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.xlsx",
			dmDocumentTitle = "Document_1.xlsx");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.xlsx",
			dmDocumentType = "Basic Document");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.mov",
			dmDocumentTitle = "Document_1.mov");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.mov",
			dmDocumentType = "Basic Document");
	}

	@priority = "5"
	@refactordone
	test CanDeleteDocumentInFolder {
		property portal.acceptance = "true";

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMFolder.addPG(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			folderName = "DM Folder Name",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.deletePG(dmDocumentTitle = "DM Document Title");
	}

	@priority = "4"
	@refactorneeded
	test CanDeleteDocumentInSitePage {
		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "/D/M/Document Title");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "/D/M/Document Title");

		Navigator.openURL();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.deletePG(dmDocumentTitle = "/D/M/Document Title");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Recycle Bin",
			portlet = "Recycle Bin");

		RecycleBin.emptyCP();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewDefault();
	}

	@priority = "5"
	@refactorneeded
	test DownloadDocumentWithSlashesInTitleViaDocumentView {
		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "/D/M/Document Title");

		DMDocument.downloadPG(dmDocumentTitle = "/D/M/Document Title");
	}

	@priority = "5"
	@refactordone
	@uittest
	test CanEditDocument {
		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			useCurrentTitle = "true");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.editCmdPG(
			checkFileNameText = "true",
			checkTitleText = "true",
			dmDocumentDescriptionEdit = "DM Document Description Edit",
			dmDocumentFileNameEdit = "DM Document File Name Edit.doc",
			dmDocumentTitle = "Document_1",
			dmDocumentTitleEdit = "DM Document Title Edit");

		PortletEntry.publish();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description Edit",
			dmDocumentFileName = "DM Document File Name Edit.doc",
			dmDocumentTitle = "DM Document Title Edit",
			dmDocumentVersionNumber = "1.1");
	}

	@priority = "5"
	@refactordone
	test CanEditSubFolder {
		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		JSONDocument.addFolder(
			dmFolderDescription = "DM SubFolder Description",
			dmFolderName = "DM SubFolder Name",
			groupName = "Guest",
			parentFolderName = "DM Folder Name");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMFolder.editPG(
			dmFolderDescriptionEdit = "DM SubFolder Description Edit",
			dmFolderName = "DM SubFolder Name",
			dmFolderNameEdit = "DM SubFolder Name Edit");
	}

	@priority = "4"
	@refactordone
	test CanMoveDocumentWithSlashesInTitle {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentTitle = "/D/M/Document Title",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.moveToFolderPG(
			dmDocumentTitle = "/D/M/Document Title",
			dmFolderName = "DM Folder Name");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "/D/M/Document Title");
	}

	@priority = "4"
	@refactordone
	test CannotMoveToFolderWithDuplicateTitle {
		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder1 Description",
			dmFolderName = "DM Folder1 Name",
			groupName = "Guest");

		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder2 Description",
			dmFolderName = "DM Folder2 Name",
			groupName = "Guest");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			folderName = "DM Folder1 Name",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			folderName = "DM Folder2 Name",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder1 Name");

		DMDocument.moveToFolderPG(
			dmDocumentTitle = "DM Document Title",
			dmDuplicateDocument = "true",
			dmFolderName = "DM Folder2 Name");
	}

	@description = "This validates that the location of the document accurately displays after it is moved from one folder to another."
	@priority = "4"
	@refactordone
	test CanMoveDocumentFromOneFolderToAnother {
		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder1 Description",
			dmFolderName = "DM Folder1 Name",
			groupName = "Guest");

		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder2 Description",
			dmFolderName = "DM Folder2 Name",
			groupName = "Guest");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			folderName = "DM Folder1 Name",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder1 Name");

		DMDocument.moveToFolderPG(
			dmDocumentTitle = "DM Document Title",
			dmFolderName = "DM Folder2 Name");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder2 Name");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			dmFolderName = "DM Folder2 Name",
			locationFieldName = "true");
	}

	@priority = "5"
	@refactordone
	test CanRemovePortlet {
		property portal.acceptance = "true";

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Portlet.deletePG(portletName = "Documents and Media");
	}

	@priority = "4"
	@refactordone
	test CanRevertFile {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.editPG(
			dmDocumentFileEdit = "Document_2.doc",
			dmDocumentTitle = "DM Document Title Version 2");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title Version 2",
			dmDocumentVersionNumber = "2.0");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.revertPG(
			dmDocumentTitle = "DM Document Title",
			dmDocumentVersionNumber = "1.0");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			dmDocumentVersionNumber = "3.0");
	}

	@description = "This test covers LPS-102179. It ensures that the user can create a shortcut using a file from a different site after setting the root folder."
	@priority = "3"
	@refactorneeded
	test ShortcutCanBeAddedToDocumentAfterSettingRootFolder {
		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "Test Folder",
			groupName = "Site Name");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "text/plain",
			sourceFileName = "Document_1.txt");

		Navigator.openSiteURL(siteName = "Site Name");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.openSiteURL(siteName = "Site Name");

		DMDocument.setRootFolder(dmRootFolderName = "Test Folder");

		Navigator.openSiteURL(siteName = "Site Name");

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		DMDocument.addDMShortcutPG(
			dmDocumentTitle = "DM Document Title",
			siteName = "${siteName}");

		Navigator.openSiteURL(siteName = "Site Name");

		DMDocument.viewPG(dmDocumentTitle = "DM Document Title");
	}

	@priority = "4"
	@refactorneeded
	test UnlockDocumentInFolder {
		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			folderName = "DM Folder Name",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.checkoutPG(dmDocumentTitle = "DM Document Title");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.viewCheckedoutPG(
			dmDocumentTitle = "DM Document Title",
			status = "Draft");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.checkinPG(dmDocumentTitle = "DM Document Title");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.viewAllPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			dmDocumentVersionNumber = "2.0");
	}

	@description = "This test covers LPS-91595. It ensures that the document selection persists when navigating through the pagination in the DM widget."
	@priority = "4"
	@refactorneeded
	test ViewDocumentPagination {
		for (var documentName : list "Document_2.doc,Document_1.xls,Document_4.jpg,Document_3.jpg,Document_2.xls,Document_1.txt,Document_4.txt,Document_3.xls,Document_2.jpg,Document_1.doc") {
			var extension = StringUtil.extractLast("${documentName}", ".");

			if ("${extension}" == "doc") {
				var mimeType = "application/msword";
			}
			else if ("${extension}" == "jpg") {
				var mimeType = "image/jpg";
			}
			else if ("${extension}" == "xls") {
				var mimeType = "application/vnd.ms-excel";
			}
			else if ("${extension}" == "txt") {
				var mimeType = "text/plain";
			}

			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "${documentName}",
				groupName = "Guest",
				mimeType = "${mimeType}",
				sourceFileName = "${documentName}");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Pagination.changePagination(itemsPerPage = "4");

		Pagination.viewResults(results = "Showing 1 to 4 of 10 entries.");

		DMDocument.checkDocumentCheckboxPG(dmDocumentTitle = "Document_2.jpg");

		AssertTextEquals(
			locator1 = "ManagementBar#NAVBAR_TEXT",
			value1 = "1 of 10 Selected");

		Click(locator1 = "Pagination#NEXT_LINK");

		DMDocument.checkDocumentCheckboxPG(dmDocumentTitle = "Document_4.jpg");

		AssertTextEquals(
			locator1 = "ManagementBar#NAVBAR_TEXT",
			value1 = "2 of 10 Selected");
	}

}