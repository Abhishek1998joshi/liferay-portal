@component-name = "portal-change-list"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Change List";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		GlobalMenu.gotoPortlet(
			category = "Change Lists",
			panel = "Applications",
			portlet = "Settings");

		ChangeList.enableChangeList();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			GlobalMenu.gotoPortlet(
				category = "Change Lists",
				panel = "Applications",
				portlet = "Settings");

			ChangeList.disableChangeList();
		}
	}

	@priority = "3"
	test CancelPublicationCreation {
		property test.name.skip.portal.instance = "ChangeListManagePublications#CancelPublicationCreation";

		ChangeList.cancelPublicationCreation();
	}

	@priority = "3"
	test CreateNewPublicationWithLongName {
		property test.name.skip.portal.instance = "ChangeListManagePublications#CreateNewPublicationWithLongName";

		ChangeList.createNewPublication(
			gotoManagePublications = "true",
			publicationName = "Lopadotemachoselachogaleokranioleipsanodrimhypotrimmatosilphioparaomelitokatakechymenokichlepikossyphophattoperisteralektryonoptekephalliokigklopeleiolagoiosiraiobaphetraganopterygon");

		Alert.viewLengthValidationField();
	}

	@priority = "3"
	test CreateNewPublicationWithoutDescription {
		property test.name.skip.portal.instance = "ChangeListManagePublications#CreateNewPublicationWithoutDescription";

		ChangeList.createNewPublication(
			gotoManagePublications = "true",
			publicationName = "Publication Name");

		AssertTextPresent(
			locator1 = "ChangeList#CHANGE_LIST_INDICATOR_TITLE",
			value1 = "Publication Name");
	}

	@priority = "3"
	test CreateNewPublicationWithoutName {
		property test.name.skip.portal.instance = "ChangeListManagePublications#CreateNewPublicationWithoutName";

		ChangeList.createNewPublication(gotoManagePublications = "true");

		Alert.viewRequiredField();
	}

	@priority = "5"
	test DeletePublication {
		property test.name.skip.portal.instance = "ChangeListManagePublications#DeletePublication";

		ChangeList.createNewPublication(
			gotoManagePublications = "true",
			publicationName = "Publication Name");

		ChangeList.deletePublication(publicationName = "Publication Name");
	}

}