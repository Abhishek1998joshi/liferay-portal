@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Administration";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-117983. No alert will popup when add a web content based on structure with XSS name."
	@priority = "4"
	test AddWebContentBasedOnStructureNameWithXSS {
		task ("Add a structure with XSS name") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(structureName = '''<script>alert(123);</script>''');

			DataEngine.addField(
				fieldFieldLabel = "Text",
				fieldName = "Text");

			WebContentStructures.saveCP(structureName = '''<script>alert(123);</script>''');
		}

		task ("View the structure is shown") {
			WebContentStructures.viewCP(structureName = '''<script>alert(123);</script>''');
		}

		task ("View no alert popup when add a web content based on new structure") {
			NavItem.click(navItem = "Web Content");

			WebContentNavigator.gotoAddWithStructureCP(structureName = '''&lt;script&gt;alert(123);&lt;/script&gt;''');

			WebContent.addWithStructureCP(
				structureName = '''<script>alert(123);</script>''',
				webContentTitle = "Web Content Title");

			PortletEntry.publish();
		}
	}

}